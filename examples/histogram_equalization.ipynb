{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "6ea02393",
   "metadata": {},
   "source": [
    "# Histogram Equalization Examples\n",
    "\n",
    "Comparing the Python and Julia implementations of the CLAHE code."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f0a11eef",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Pkg\n",
    "Pkg.add(\"IceFloeTracker\")\n",
    "Pkg.add(\"PyCall\", \"Images\", \"DataFrames\")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "5474b114",
   "metadata": {},
   "source": [
    "Standard imports:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "41c0ce2f",
   "metadata": {},
   "outputs": [],
   "source": [
    "using IceFloeTracker, Images, DataFrames\n",
    "import IceFloeTracker.Filtering: sk_exposure, adjust_histogram, ContrastLimitedAdaptiveHistogramEqualization\n",
    "using PyCall\n",
    "@pyimport numpy as np"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2938abe2",
   "metadata": {},
   "source": [
    "Use the standard dataset:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "5729e161",
   "metadata": {},
   "outputs": [],
   "source": [
    "dataset = Watkins2026Dataset()"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a20544e4",
   "metadata": {},
   "source": [
    "## Histogram Equalization\n",
    "We call `adjust_histogram` with `Equalization` implemented in Julia and `sk_exposure.equalize_hist` implemented in Python, and compare the results."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "8f0ed384",
   "metadata": {},
   "outputs": [],
   "source": [
    "case = dataset[21]\n",
    "casered = red.(modis_truecolor(case))\n",
    "img_eq_jl = adjust_histogram(casered, Equalization(; nbins=128))\n",
    "img_eq_py = sk_exposure.equalize_hist(np.array(casered); nbins=128)\n",
    "difference = float.(img_eq_jl) .- float.(img_eq_py)\n",
    "display(name(case))\n",
    "display(\n",
    "    hcat(Gray.(casered),\n",
    "        Gray.(img_eq_py),\n",
    "        Gray.(img_eq_jl),\n",
    "        Gray.(0.5 .+ difference .* 5))\n",
    ")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "6a1d0e24",
   "metadata": {},
   "source": [
    "To compare many cases, we need a function. \n",
    "\n",
    "In general, we want to compare \n",
    "- the status quo Python result to the original, \n",
    "- the original to the new implementation, \n",
    "- and the difference between the two results.\n",
    "\n",
    "We use the `compare` function to calculate the following images shown from left to right in the example below:\n",
    "- Python output\n",
    "- difference Python vs original,\n",
    "- original,\n",
    "- difference Julia vs original,\n",
    "- Julia output\n",
    "- difference Julia vs Python.\n",
    "\n",
    "Differences are shown as offsets from a half-gray tone, so flat gray means no difference. "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0629e878",
   "metadata": {},
   "outputs": [],
   "source": [
    "function compare(\n",
    "    img;\n",
    "    JlAlgorithm=Equalization(; nbins=128),\n",
    "    py_function=sk_exposure.equalize_hist,\n",
    "    py_kwargs=Dict(:nbins => 128),\n",
    "    difference_offset=0.5,\n",
    "    difference_factor=1.0\n",
    ")\n",
    "\n",
    "    img_eq_jl = adjust_histogram(img, JlAlgorithm)\n",
    "    img_eq_py = py_function(np.array(img); py_kwargs...)\n",
    "    \n",
    "    im_diff_py = float.(img) .- float.(img_eq_py)\n",
    "    im_diff_jl = float.(img) .- float.(img_eq_jl)\n",
    "    jl_diff_py = float.(img_eq_jl) .- float.(img_eq_py)\n",
    "\n",
    "    result = (\n",
    "        py=Gray.(img_eq_py),\n",
    "        im_diff_py=Gray.(0.5 .+ ((im_diff_py) * difference_factor)),\n",
    "        im=Gray.(img),\n",
    "        im_diff_jl=Gray.(0.5 .+ ((im_diff_jl) * difference_factor)),\n",
    "        jl=Gray.(img_eq_jl),\n",
    "        jl_diff_py=Gray.(0.5 .+ ((jl_diff_py) * difference_factor)),\n",
    "    )\n",
    "    return result\n",
    "\n",
    "end\n",
    "\n",
    "function compare(dataset::Dataset; kwargs...)\n",
    "    results = []\n",
    "    for case in dataset\n",
    "        @info \"Processing case: $(name(case))\"\n",
    "        img = modis_truecolor(case)\n",
    "        channel1 = red.(img)\n",
    "        img_results = compare(channel1; kwargs...)\n",
    "\n",
    "        img_eq_jl = img_results.jl\n",
    "        img_eq_py = img_results.py\n",
    "\n",
    "        result = (name=name(case),\n",
    "            ssim_original_py=assess_ssim(channel1, img_eq_py),\n",
    "            ssim_original_jl=assess_ssim(channel1, img_eq_jl),\n",
    "            ssim_jl_py=assess_ssim(img_eq_jl, img_eq_py),\n",
    "            img_results...)\n",
    "        push!(results, result)\n",
    "    end\n",
    "    results_df = DataFrame(results)\n",
    "    return results_df\n",
    "end\n",
    "\n",
    "JlAlgorithmHE = Equalization(; nbins=128)\n",
    "py_function_he = sk_exposure.equalize_hist\n",
    "py_kwargs_he = Dict(:nbins => 128)\n",
    "\n",
    "display(hcat(compare(red.(modis_truecolor(dataset[174*2])); JlAlgorithm=JlAlgorithmHE, py_function=py_function_he, py_kwargs=py_kwargs_he)...))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "711a21c3",
   "metadata": {},
   "source": [
    "We next look at the full validation dataset. \n",
    "We want to find cases where the Julia output is maximally different from the Python output. \n",
    "To find cases which are perceptually different, we use the structural similarity index measure (SSIM),\n",
    "where 1 is identical and 0 is completely different."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "31c18439",
   "metadata": {},
   "outputs": [],
   "source": [
    "results_he = compare(dataset; JlAlgorithm=JlAlgorithmHE, py_function=py_function_he, py_kwargs=py_kwargs_he);"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "889d44c1",
   "metadata": {},
   "source": [
    "The \"worst\" cases are shown below.\n",
    "\n",
    "- The Julia outputs in very low contrast, open water regions are perhaps better than the python, retaining the average very dark color more consistently. \n",
    "- In high-contrast parts of the image the performance seems similar. "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f7685ebf",
   "metadata": {},
   "outputs": [],
   "source": [
    "for row in eachrow(sort(results_he, :ssim_jl_py))[1:20]\n",
    "    display(\"$(row.name), ssim=$(round(row.ssim_jl_py, digits=2))\")\n",
    "    display(hcat(row.im, row.py, row.jl, row.jl_diff_py))\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a0191c2a",
   "metadata": {},
   "source": [
    "## Contrast Limited Adaptive Histogram Equalization\n",
    "We call `adjust_histogram` with `ContrastLimitedAdaptiveHistogramEqualization` implemented in Julia and `sk_exposure.equalize_adapthist` implemented in Python, and compare the results."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "863c15fd",
   "metadata": {},
   "outputs": [],
   "source": [
    "case = dataset[21]\n",
    "casered = red.(modis_truecolor(case))\n",
    "img_eq_jl = adjust_histogram(casered, ContrastLimitedAdaptiveHistogramEqualization(; clip=2.0))\n",
    "img_eq_py = sk_exposure.equalize_adapthist(np.array(casered); clip_limit=0.01)\n",
    "difference = img_eq_jl .- img_eq_py\n",
    "display(name(case))\n",
    "display(\n",
    "    hcat(Gray.(casered), \n",
    "    Gray.(img_eq_py ), \n",
    "    Gray.(img_eq_jl ), \n",
    "    Gray.(0.5 .+ difference .* 5))\n",
    ")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "26a142df",
   "metadata": {},
   "source": [
    "Using the `compare` utility:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a8313c5e",
   "metadata": {},
   "outputs": [],
   "source": [
    "JlAlgorithmCLAHE = ContrastLimitedAdaptiveHistogramEqualization(; clip=2.0, nbins=256)\n",
    "py_function_clahe = sk_exposure.equalize_adapthist\n",
    "py_kwargs_clahe = Dict(:clip_limit => 0.01)\n",
    "\n",
    "display(hcat(compare(casered; JlAlgorithm=JlAlgorithmCLAHE, py_function=py_function_clahe, py_kwargs=py_kwargs_clahe)...));"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "150915d9",
   "metadata": {},
   "source": [
    "We next look at the full validation dataset. \n",
    "We want to find cases where the Julia output is maximally different from the Python output. \n",
    "To find cases which are perceptually different, we use the structural similarity index measure (SSIM),\n",
    "where 1 is identical and 0 is completely different."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "25351610",
   "metadata": {},
   "outputs": [],
   "source": [
    "results_clahe = compare(dataset; JlAlgorithm=JlAlgorithmCLAHE, py_function=py_function_clahe, py_kwargs=py_kwargs_clahe);"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "4d03886f",
   "metadata": {},
   "source": [
    "The \"worst\" cases are shown below.\n",
    "\n",
    "The image order is:\n",
    "- Original\n",
    "- Python result,\n",
    "- Julia result,\n",
    "- Python vs Julia difference, scaled as differences around half-gray.\n",
    "\n",
    "The results from Julia are very similar to those from Python."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c84082b4",
   "metadata": {},
   "outputs": [],
   "source": [
    "for row in eachrow(sort(results_clahe, :ssim_jl_py))[1:20]\n",
    "    display(\"$(row.name), ssim=$(round(row.ssim_jl_py, digits=2))\")\n",
    "    display(hcat(row.im, row.py, row.jl, row.jl_diff_py))\n",
    "end"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.8",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.8"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
